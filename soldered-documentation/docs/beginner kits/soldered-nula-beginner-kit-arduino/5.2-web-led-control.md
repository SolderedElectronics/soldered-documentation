---
slug: /soldered-nula-beginner-kit-arduino/web-led-control
title: 5.2 Web LED Control
id: nula-beginner-arduino-web-led-control
hide_title: False
---

<CenteredImage src="/img/under_construction.png" alt="placeholder" caption="Full example video" width="600px"/>

The goal of this example is to show you how to **control an LED on the NULA MINI board through a web interface**.  
Using built-in WiFi, the board hosts a **web page** that allows you to turn the LED **ON or OFF** and view its **real-time status** directly from your computer or phone’s browser.

<SuccessBox>

**In this documentation you will learn:**
- How to set up a **Wi-Fi web server** on the NULA MINI board.  
- How to serve a **custom HTML webpage** stored in program memory.  
- How to handle HTTP requests for interactive LED control.  
- How to use **JavaScript** in the webpage for **real-time status updates**.

</SuccessBox>

**Hardware required:**
- 1× Soldered NULA MINI  
- 1× LED (any color)  
- 1× 330Ω resistor   
- 1× Breadboard  
- Jumper wires  
- USB-C cable  

<CenteredImage src="/img/nula-beginner-kit-arduino/1_1components.jpg" alt="All components" caption="All components needed for this example" />
---

## Putting the components together

### 1. Connect the LED
We will start by connecting the LED the same way as in [**1.1 LED Blinking**](/documentation/soldered-nula-beginner-kit-arduino/led-blinking).  

- Connect the **anode** (long leg) of the LED through a **330Ω resistor** to **IO04** on the NULA MINI board.  
- Connect the **cathode** (short leg) directly to **GND**.

<CenteredImage src="/img/nula-beginner-kit-arduino/connection5.jpg" alt="Step 5 of connections" caption="Step: 1." width="600px"/>

---


## Setting up the code

Start by including the necessary WiFi and web server libraries, and defining your WiFi credentials.
```cpp
#include <WiFi.h>   //WiFi library
#include <WebServer.h>  //WebServer library

const char* ssid = "Your SSID";
const char* password = "Your password";

const int LED_PIN = 4;  //Pin number where the LED is connected

WebServer server(80);   //Create a web server on port 80
```
---

## The Webpage

The HTML page is stored directly in program memory `PROGMEM` and contains simple buttons and JavaScript logic to update LED status in real time.

```cpp
const char htmlPage[] PROGMEM = R"rawliteral(
<!DOCTYPE html>
<html>
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <title>NULA MINI LED Control</title>
  <style>
    body { font-family: Arial; text-align: center; margin-top: 50px; }
    button { padding: 15px 30px; margin: 10px; font-size: 20px; }
    .status { font-size: 24px; margin-top: 20px; }
  </style>
</head>
<body>
  <h1>NULA MINI LED Control</h1>
  <button onclick="fetch('/led/on').then(()=>updateStatus())">ON</button>
  <button onclick="fetch('/led/off').then(()=>updateStatus())">OFF</button>
  <div class="status" id="status">Loading status...</div>

  <script>
    // Function that requests LED status from the NULA MINI and updates the page
    async function updateStatus() {
      let res = await fetch('/led/status');
      let text = await res.text();
      document.getElementById('status').innerHTML = 'LED is ' + text.toUpperCase();
    }

    // Run immediately after page load and update every 2 seconds
    updateStatus();
    setInterval(updateStatus, 2000);
  </script>
</body>
</html>
)rawliteral";

```

---

## Setting up the web server

The following handlers define how the NULA MINI resonds to each URL request:

```cpp
void handleRoot(){
    server.send(200, "text/html", htmlPage); //Serve the webpage
}

void handleLedOn(){
    digitalWrite(LED_PIN,HIGH);
    server.send(200,"text/plain", "on");
}

void handleLedOff(){
    digitalWrite(LED_PIN,LOW);
    server.send(200, "text/plain", "off");
}

void handleLedStatus(){
    String status = digitalRead(LED_PIN)? "on" : "off";
    server.send(200,"text/plain", status);
}
```

---

## Connecting to WiFi and starting the server

In the `setup()`, we connect to WiFi and start the web server.

```cpp
void setup(){
    Serial.begin(115200);
    pinMode(LED_PIN, OUTPUT);
    digitalWrite(LED_PIN, LOW);

    Serial.println("Connecting to WiFi...");
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("\nWiFi connected!");
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());  // Use this IP to access the web interface

  server.on("/", handleRoot);
  server.on("/led/on", handleLedOn);
  server.on("/led/off", handleLedOff);
  server.on("/led/status", handleLedStatus);

  server.begin();
  Serial.println("HTTP server started");
}
```

The `loop()` continuously listens for incoming requests:

```cpp
void loop(){
    server.handleClient();
}
```

---

## Accessing the web interface

After uploading the code and opening the Serial Monitor (115200 baud), look for a like:

<CenteredImage src="/img/nula-beginner-kit-arduino/ipaddress.png" alt="NULA MINI LED control web page" caption="NULA MINI LED control web page" width="400px"/>

Enter the IP address in your computer's browser. You'll see a simple control panel to turn the LED ON or OFF and view its live status.

<CenteredImage src="/img/nula-beginner-kit-arduino/webpage.png" alt="NULA MINI LED control web page" caption="NULA MINI LED control web page" width="600px"/>

## How it works
This example turns your NULA MINI board into a **mini web server** that you can access from any device connected to the same local WiFi network. When you open the server's IP address in a browser, you'll see a webpage with:

- Two buttons: **ON** and **OFF**
- A **status indicator** showing the current state of the LED

When you click a button, the page sends an **HTTP request** to the board:
- Visiting `/led/on` turns the LED on
- Visiting the `led/off` turns the LED off

---
