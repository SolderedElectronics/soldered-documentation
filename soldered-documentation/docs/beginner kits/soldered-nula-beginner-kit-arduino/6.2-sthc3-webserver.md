---
slug: /soldered-nula-beginner-kit-arduino/shtc3-webserver
title: 6.2 SHTC3 Web Server
id: nula-beginner-arduino-shtc3-webserver
hide_title: False
---

<CenteredImage src="/img/under_construction.png" alt="placeholder" caption="Full example video" width="600px"/>

The goal of this example is to show you how to use the **SHTC3 temperature and humidity sensor** with the **NULA MINI board** to create a **live web server**.  
The board connects to a Wi-Fi network, reads sensor data every few seconds, and hosts a simple web page that shows **live temperature and humidity** values that update automatically.

<SuccessBox>

**In this documentation you will learn:**
- How to connect the **SHTC3 sensor** to the NULA MINI via **I²C**.  
- How to connect to a Wi-Fi network using the `WiFi` library.  
- How to host a basic **web server** that displays sensor data.  
- How to use **JavaScript** to automatically update values on a web page using `fetch()` and JSON.

</SuccessBox>

**Hardware required:**
- 1× Soldered NULA MINI  
- 1× SHTC3 temperature and humidity sensor (Qwiic)  
- 1× Qwiic cable  
- USB-C cable  

<CenteredImage src="/img/nula-beginner-kit-arduino/6_2components.jpg"  caption="All components that are needed for this example." />
---

## Putting the components together

### 1. Connect the SHTC3 sensor
Use a **Qwiic cable** to connect the SHTC3 sensor directly to the NULA board.  
The Qwiic connector automatically provides **power (3.3V)**, **GND**, and the **I²C communication lines (SDA, SCL)**.

<CenteredImage src="/img/nula-beginner-kit-arduino/connection6_2_1.jpg"  width="600px" caption="Step: 1." />

<InfoBox>
For a refresher on how I2C communication works, check out  
[**4.1 Show Message on LCD on Button Press**](/documentation/soldered-nula-beginner-kit-arduino/lcd-button-message/#basics-of-i2c-communication).
</InfoBox>

---

## How it works

## Understanding how it works

This project combines **sensor measurement** with **Wi-Fi web hosting**.  
Here’s what happens:

1. The NULA board connects to your **Wi-Fi network** using the provided SSID and password.  
2. It starts a **web server** on port 80 (the default HTTP port).  
3. The **SHTC3 sensor** is read every **2 seconds**, and the new values are stored in variables.  
4. When a user opens the web page in a browser, the page automatically requests the latest readings using JavaScript and updates them live — no need to refresh manually!

---

## Setting up the code

Include necessary libraries for I2C communication, WiFi and webserver capabilities, create shtc3 object and start the server:

```cpp
#include <Wire.h>
#include <WiFi.h>
#include <WebServer.h>
#include "SHTC3-SOLDERED.h"

// Wi-Fi credentials
const char* ssid = "Your SSID";
const char* password = "Your password";

SHTC3 shtcSensor;
WebServer server(80);

float temperature = 0.0;
float humidity = 0.0;
unsigned long lastUpdate = 0;
const unsigned long UPDATE_MS = 2000; // Update every 2 seconds
```

## Creating the web page
The HTML code below is served directly by the board and includes a small JavaScript snippet that **fetches new data every 2 seconds** from the `/data` endpoint.

```cpp
const char MAIN_page[] PROGMEM = R"rawliteral(
<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8">
  <title>NULA MINI SHTC3 Live Data</title>
  <style>
    body { font-family: Arial; text-align: center; background: #f4f4f4; }
    h1 { color: #333; margin-top: 40px; }
    .value { font-size: 2em; color: #0078D7; }
  </style>
</head>
<body>
  <h1>NULA MINI SHTC3 Sensor</h1>
  <p>Temperature: <span id="temp" class="value">--</span> °C</p>
  <p>Humidity: <span id="hum" class="value">--</span> %</p>
  <p style="font-size:0.9em;color:#777;">(Updates automatically every 2 seconds)</p>

  <script>
    // Ask the ESP32 for new data and update the numbers on the page
    async function updateData() {
      try {
        const response = await fetch('/data');
        const json = await response.json();
        document.getElementById('temp').textContent = json.temp.toFixed(1);
        document.getElementById('hum').textContent  = json.hum.toFixed(1);
      } catch (e) {
        console.log('Error fetching data:', e);
      }
    }
    setInterval(updateData, 2000);
    updateData();
  </script>
</body>
</html>
)rawliteral";

```

## Setting up the web server
Each URL is handled by the board by the corresponding function:
```cpp

void handleRoot(){
    server.send(200,"text/html", MAIN_page);
}

void handleData(){
    String json = "{\"temp\":"+String(temperature,2)+",\"hum\":"+String(humidity,2)+"}";
    server.send(200, "application/json", json); //Send data as JSON
}
```

---

## Connecting to WiFi and starting the server
In `setup()`, we initialize the I2C bus, sensor, WiFi and web server.
```cpp
void setup() {
  Wire.begin();
  Serial.begin(115200);
  delay(500);

  if (!shtcSensor.begin()) {
    Serial.println("SHTC3 init failed!");
    while (1) delay(10);
  }

  Serial.println("Connecting to WiFi...");
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("\nWiFi connected!");
  Serial.print("NULA IP address: ");
  Serial.println(WiFi.localIP());

  server.on("/", handleRoot);
  server.on("/data", handleData);
  server.begin();
  Serial.println("Web server started!");
}
```

---

## Taking measurements and updating the webpage

Every 2 seconds, new sensor readings are taken and stored in the `temperature` and `humidity` variables. Meanwhile, the web server continues serving live data to any connected browser.

```cpp
void loop() {
  server.handleClient();

  unsigned long now = millis();
  if (now - lastUpdate >= UPDATE_MS) {
    lastUpdate = now;
    shtcSensor.sample();
    temperature = shtcSensor.readTempC();
    humidity = shtcSensor.readHumidity();

    Serial.print("Temp: ");
    Serial.print(temperature, 2);
    Serial.print(" °C, Hum: ");
    Serial.print(humidity, 2);
    Serial.println(" %");
  }
}
```

---

## Accessing the web page
After uploading the sketch, open the Serial Monitor at 115200 baud. When the NULA MINI connects to WiFi, it will display its IP address:

<CenteredImage src="/img/nula-beginner-kit-arduino/6_2ipaddress.png"  caption="Serial monitor output." width="400px" />

Open this IP address in a browser and the webpage should be running:

<CenteredImage src="/img/nula-beginner-kit-arduino/6_2webpage.png"  caption="NULA MINI Temperature and Humidity monitoring webpage." width="600px" />


---

## JSON data exchange
When the page loads, the JavaScript repetedly sends a **GET request** to `/data`. The NULA MINI responds with a JSON object: `{"temp":23.54,"hum":46.73}`
The browser extarcts these values and updates the HTML elements in real time. This method of exchanging small bits of data between MCU and a web page is called **AJAX** (Asynchronous javaScript and XML).

<InfoBox> Using JSON makes it easy for web pages or other IoT devices to parse and reuse sensor data.</InfoBox>

---

## Full example
<CenteredImage src="/img/under_construction.png" alt="placeholder" caption="Full example video" width="600px"/>

Check out the full example code on the link below:
<QuickLink title="6.2_SHTC3_WebServer.ino" description="Example that measures temperature and humidity with the SHTC3 sensor and displays live data on a web page using Wi-Fi." url="https://github.com/SolderedElectronics/Soldered-NULA-Beginner-kit-Arduino-project-examples/blob/main/6_Environmental_Measurement/6.2_SHTC3_WEBServer/6.2_SHTC3_WEBServer.ino" />