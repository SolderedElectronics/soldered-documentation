---
slug: /soldered-nula-beginner-kit-arduino/shtc3-lcd-display
title: 6.1 Measure Temperature and Humidity with SHTC3 and LCD
id: nula-beginner-arduino-shtc3-lcd-display
hide_title: False
---

<CenteredImage src="/img/under_construction.png" alt="placeholder" caption="Full example video" width="600px"/>

The goal of this example is to show you how to use the **SHTC3 temperature and humidity sensor** together with a **16×2 LCD**.  
The NULA board reads the sensor values and displays them both on the **Serial Monitor** (for debugging) and on the **LCD** (for standalone use).

<SuccessBox>

**In this documentation you will learn:**
- How to connect the **SHTC3 sensor** and an **LCD display** via Qwiic (I²C).  
- How to install and use the **Soldered SHTC3 library**.  
- How to periodically read sensor values using `millis()`.  
- How to display the values on the **Serial Monitor** and **LCD**.

</SuccessBox>

**Hardware required:**
- 1× Soldered NULA MINI board  
- 1× SHTC3 temperature and humidity sensor (Qwiic)  
- 1× 16×2 Qwiic LCD display  
- 2× Qwiic connectors  
- USB-C cable  

<CenteredImage src="/img/nula-beginner-kit-arduino/6_1components.jpg"  caption="All components that are needed for this example." />

---

## Putting the components together

Both the **SHTC3 sensor** and the **LCD** use the I²C protocol and connect through **Qwiic cables**. You can daisychain the components together. Connect NULA board to SHTC3 via Qwiic cable, using the other available Qwiic connector, connect the display.

<CenteredImage src="/img/nula-beginner-kit-arduino/connection6_1_1.jpg" width="600px" caption="Step 1." />

---

## Setting up the libraries

This example requires two additional Soldered libraries:  

[**SHTC3 Library**](https://github.com/SolderedElectronics/Soldered-SHTC3-Temperature-Humidity-Sensor-Arduino-Library)  
[**LCD Library**](https://github.com/SolderedElectronics/Soldered-LCD-Library)  

Check out the [**4.1 Show Message on LCD on button press**](/documentation/soldered-nula-beginner-kit-arduino/lcd-button-message/#setting-up-the-lcd) example docs for a more detailed explanation on how to download and install Arduino libraries from Github

---

## Basics of periodic updates with `millis()`

When working with sensors, it’s common to take readings at fixed intervals. Instead of using `delay()`, which stops the whole program, we use `millis()`.  

- `millis()` returns the number of milliseconds since the program started.  
- By storing the last time we took a measurement, we can check if enough time has passed before reading again.  
- This approach keeps the program responsive and avoids unnecessary delays.  

In this example, the sensor is read every **2000 ms (2 seconds)**.

---

## Initializing the sensor and display

In the **setup()**, we initialize everything:  

```cpp
#include <Wire.h>
#include "SHTC3-SOLDERED.h"
#include "LCD-SOLDERED.h"

SHTC3 shtcSensor;       // Sensor object
LCD lcd(16, 2);         // LCD object (16 columns, 2 rows)

void setup() {
  Wire.begin();               // Start I2C
  Serial.begin(115200);       // Serial monitor for debugging

  if (!shtcSensor.begin()) {  // Initialize the sensor
    Serial.println("SHTC3 init failed!");
  }

  lcd.begin();                // Initialize LCD
  lcd.backlight();            // Turn on backlight
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("SHTC3 + LCD");
  lcd.setCursor(0, 1);
  lcd.print("Starting...");
  delay(800);                 // Show startup message
  lcd.clear();
}
```

---

## Displaying sensor data

Every 2 seconds, we take a new measurement and show it on both the Serial Monitor and the LCD.

```cpp
const unsigned long UPDATE_MS = 2000;
unsigned long lastUpdate = 0;

void loop() {
  unsigned long now = millis();

  if (now - lastUpdate >= UPDATE_MS) {
    lastUpdate = now;

    shtcSensor.sample();                 // Take a measurement
    float tC  = shtcSensor.readTempC();  // Temperature in °C
    float hum = shtcSensor.readHumidity(); // Humidity in %

    // --- Serial output ---
    Serial.print("Temp: ");
    Serial.print(tC, 2);
    Serial.print(" C, Hum: ");
    Serial.print(hum, 2);
    Serial.println(" %");

    // --- LCD output ---
    lcd.setCursor(0, 0);
    lcd.print("Temp: ");
    lcd.print(tC, 1);    // Print with 1 decimal
    lcd.print(" C   ");  // Extra spaces clear leftovers

    lcd.setCursor(0, 1);
    lcd.print("Hum:  ");
    lcd.print(hum, 1);
    lcd.print(" %   ");
  }
}

```
---

<CenteredImage src="/img/nula-beginner-kit-arduino/6_1display.jpg" width="600px" caption="Running the example" />

## Full example

Check out the full example code on the link below:
<QuickLink 
  title="6.1_SHTC_LCD.ino" 
  description="Example that shows how to measure temperature and humidity with the SHTC3 sensor and display the values both on the Serial Monitor and on a 16x2 LCD."
  url="https://github.com/SolderedElectronics/Soldered-NULA-Beginner-kit-Arduino-project-examples/blob/main/6_Environmental_Measurement/6.1_SHTC3_LCD/6.1_SHTC3_LCD.ino" 
/>
